(ns shousetsu.serialization-test
  (:require [kotoba.lang.text :as str]
            [clojure.test :refer [deftest is]]
            [shousetsu.serialization :as serialization]))

(deftest entity-ids-round-trip
  (is (= "author:akagi-rin" (serialization/author-id "akagi-rin")))
  (is (= "work:reincarnated-librarian" (serialization/work-id "reincarnated-librarian")))
  (is (= "episode:reincarnated-librarian:1" (serialization/episode-id "reincarnated-librarian" 1)))
  (is (= "world:reincarnated-librarian" (serialization/world-id "reincarnated-librarian")))
  (is (= "char:reincarnated-librarian:sara" (serialization/char-id "reincarnated-librarian" "sara")))
  (is (= "review:abc123" (serialization/review-id "abc123")))
  (is (= "reincarnated-librarian"
         (serialization/work-slug-of (serialization/work-id "reincarnated-librarian"))))
  (is (= "reincarnated-librarian"
         (serialization/work-slug-of (serialization/episode-id "reincarnated-librarian" 7))))
  (is (= 7 (serialization/episode-index-of (serialization/episode-id "reincarnated-librarian" 7))))
  (is (nil? (serialization/episode-index-of "work:xyz"))))

(deftest slug-derivation
  (is (= "reincarnated-librarian" (serialization/slug "Reincarnated Librarian")))
  (is (str/starts-with? (serialization/slug "赤城凛") "x"))
  (is (= (serialization/slug "赤城凛") (serialization/slug "赤城凛"))))

(deftest tx-helpers
  (is (= "[:db/add \"author:akagi-rin\" :au/penName \"赤城凛\"]"
         (serialization/encode
          (serialization/tx-add "author:akagi-rin" "au/penName" "赤城凛"))))
  (let [ops (mapv #(serialization/tx-add (str "work:w" %) "nv/title"
                                         (apply str (repeat 100 "x")))
                  (range 20000))
        chunks (serialization/chunk-tx-data ops 900000)]
    (is (> (count chunks) 1))
    (is (every? #(<= #?(:clj (count (.getBytes ^String % "UTF-8"))
                        :cljs (.-length (.encode (js/TextEncoder.) %)))
                     900000)
                chunks))))

(deftest record->ops-shapes
  (let [flat (map serialization/encode
                  (serialization/work->ops {:work_id "work:test" :title "T"
                                            :author_id "author:a"
                                            :status "serializing"
                                            :tags ["異世界" "内政"]}))]
    (is (some #(str/includes? % ":nv/type \"Work\"") flat))
    (is (some #(str/includes? % ":nv/id \"work:test\"") flat))
    (is (= 2 (count (filter #(str/includes? % ":nv/tag") flat)))))
  (let [flat (map serialization/encode
                  (serialization/author->ops {:author_id "author:a"
                                              :pen_name "赤城凛"
                                              :genre_affinity "異世界"}))]
    (is (some #(str/includes? % ":nv/type \"Author\"") flat))
    (is (some #(str/includes? % ":au/penName") flat))
    (is (not-any? #(str/includes? % ":au/voice") flat)))
  (let [flat (apply str (map serialization/encode
                             (serialization/episode-meta->ops
                              {:episode_id "episode:test:1" :work_id "work:test"
                               :index 1 :title "第一話" :body_blob_key "deadbeef"
                               :char_count 3000 :status "published"})))]
    (is (str/includes? flat ":ep/bodyBlobKey"))
    (is (not (str/includes? flat ":ep/body ")))))
